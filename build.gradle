import io.ossim.gradleDefaults.GrailsCommonConfig

buildscript {
    repositories {
        mavenLocal()
        if (!System.getenv('GRADLE_OFFLINE')) {
            maven { url "https://repo.grails.org/grails/core" }
            jcenter()
        }
    }
    dependencies {
        classpath('io.ossim:gradleDefaults:1.0') {
            version {
                branch = 'dev'
            }
        }
    }
}

subprojects { project ->
    boolean isGrailsApp = project.name.endsWith("-app")
    boolean isGrailsPlugin = project.name.endsWith("-plugin")
    boolean isGrailsProject = isGrailsApp || isGrailsPlugin

    version versionNumber
    group groupName

    if (isGrailsProject) {

        GrailsCommonConfig.configureSubproject((Project) project, isGrailsApp, isGrailsPlugin)

        bootRun {
            jvmArgs('-Dspring.output.ansi.enabled=always')
            addResources = true
            String springProfilesActive = 'spring.profiles.active'
            systemProperty springProfilesActive, System.getProperty(springProfilesActive)
        }

    }

}

apply plugin: 'java-base'
apply plugin: 'io.ossim.gradleDefaults'

